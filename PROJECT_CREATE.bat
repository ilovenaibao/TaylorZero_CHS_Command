echo create project......
@echo off
:: *******************************************************************************************************************************
::     **********************************************J真x工程配置！*****************************************************
::        ******************************************否t工程建可能出e**************************************************
rem readme
rem (*) ----> 必修改或者填的热
rem (/) ----> 可以使用默J值
rem 如果apk生成成功，那N在工程根目下生成：make_project_info.txt
rem @txt中的是工程相P信息
rem END
:: *******************************************************************************************************************************
:: M行工程配置

:: 0. O置所需路 ----> Initialize some info

:: 工程配置
:: (*) 工程名 (*)
set Project_Name=%1
:: (*) 工程包名 (*)
set ProjectPackage_Last_Name=%2
set ProjectPackage_Name=%3
:: (*) 工程所用adk-addon target ID (*)
set ProjectSDK_ID=%4
:: (/) 工程打包的Y源文件名 (/)
set ProjectRes_Name=%5
:: (/) 工程生成的未名的apk名 (/)
set ProjectUnsigedApk_Name=%6
:: (/) 工程最K出的apk名 (/)
set ProjectOutApk_Name=%7
:: *******************************************************************************************************************************

:: keystore使用的人信息配置
:: (/) 工程生成的keystore名 (/)
set ProjectKeyStore_Name=%8
:: (*) 生成的keystore有效日期天 (*)
set ValidityDays_Count=%9
:: (/) 你的名字姓氏 (/)
SHIFT /8
set RD_Name=%9
:: (/) 你的M挝幻Q (/)
SHIFT /8
set Company_Name=%9
:: (/) 你的M名Q (/)
SHIFT /8
set Organization_Name=%9
:: (/) 你所在域或^域名Q (/)
SHIFT /8
set Local_Name=%9
:: (/) 你所在的州或省份 (/)
SHIFT /8
set Province_Name=%9
:: (/) 你所在家的2位家代a (/)
SHIFT /8
set Country_ID=%9
:: (/) O置生成名的apk的密a (/)
SHIFT /8
set MakeApk_Pass=%9
:: (/) O置keystore主密a(一般可以喝apk名密a一致) (/)
SHIFT /8
set KeystoreMain_Pass=%9
:: *******************************************************************************************************************************

:: Java & Android SDK 路脚渲
:: (/) O置javacgr的*.java 文件的a格式 (/)
SHIFT /8
set Javac_Encode=%9
:: (/) O置javacgr的SDK版本 (/) %19
SHIFT /8
set JavacSDK_Ver=%9
:: (*) 工程路 (*) %20
SHIFT /8
set Project_Parent_Path=%9
SHIFT /8
set Project_Path=%9
:: (*) 工程炔克用路 (*) %22
SHIFT /8
set ProjectInner_Path=%9
:: (*) JavaSDK路 (*) %23
SHIFT /8
set JavaSDK_Path=%9
:: (*) AndroidSDK路 (*) %24
SHIFT /8
set AndroidJar_Path=%9
:: (/) 工程建成功的相P信息名 (/) %25
SHIFT /8
set Success_Info=%9
:: (/) 工程g信息OutPut文件路 (/) %26
SHIFT /8
set Compile_Info_Path=%9
SHIFT /8
set Compile_Info_Full_Path=%9
SHIFT /8
set Compile_Info_File_Name=%9
set Compile_OutPut_Command=>> %Compile_Info_Full_Path%\%Compile_Info_File_Name%

:: *******************************************************************************************************************************
:: start create a android project

:: 1. 建一工程
:: -n 工程名 : cmdTest
:: -p 工程存β : .\cmdTestProject
:: -k 工程包名 : com.besta.app.cmdtest
:: -a Activity子名 : CmdTestActivity
:: -t 工程使用的平_ : 10, 如果不知道可以使用Android.bat list target 聿檎
::echo create project...
call android.bat create project -n %Project_Name% -p %Project_Name% -k %ProjectPackage_Name% -a %Project_Name% -t %ProjectSDK_ID%
if errorlevel 1 goto ERROR

:: 2. 生成R.java文件，首先需要建gen文件A
echo make "R.java" file...
cd %Project_Name%
mkdir gen
call aapt.exe p -f -m -J gen -S res -I %AndroidJar_Path% -M AndroidManifest.xml
if errorlevel 1 goto ERROR

:: 3. 生成.class文件
:: eg> javac -encoding UTF-8 -target 1.6 -bootclasspath %AndroidJar_Path% -d bin src\com\besta\app\cmdtest\*.java gen\com\besta\app\cmdtest\R.java -classpath libs\baidumapapi.jar
echo make "*.class" file...
call javac -encoding %Javac_Encode% -target %JavacSDK_Ver% -bootclasspath %AndroidJar_Path% -d bin src\%ProjectInner_Path%\*.java gen\%ProjectInner_Path%\R.java
if errorlevel 1 goto ERROR

:: 4. 生成.dex文件
echo make "*.dex" file...
call dx.bat --dex --output=%Project_Path%\bin\classes.dex %Project_Path%\bin
if errorlevel 1 goto ERROR

:: 5. 利用aapt 生成*.ap_文件, 即打包Y源文件
:: 由於]有assets文件A，因此绦兄前需要建立assets
echo package "*.ap_" for some resouse file...
mkdir assets
call aapt.exe package -f -S res -I %AndroidJar_Path% -A assets -M AndroidManifest.xml -F %Project_Path%\bin\%ProjectRes_Name%
if errorlevel 1 goto ERROR

:: 6. 生成未名的apk文件
echo make unsigner apk file...
call apkbuilder.bat %Project_Path%\bin\%ProjectUnsigedApk_Name% -v -u -z %Project_Path%\bin\%ProjectRes_Name% -f %Project_Path%\bin\classes.dex -rf %Project_Path%\src -nf %Project_Path%\libs -rj %Project_Path%\libs
if errorlevel 1 goto ERROR

:: 7. 用JAVA SDK提供的工具keytools生成名的apk文件
:: -dname 嫡f明(按序)：您的---> 名字的姓氏，M挝幻Q，M名Q，所在域或^域名Q， 州或省份， 所在家的2位家代a
:: 若不@些信息t在cmd\行的^程中提示入信息，因此成下面配置好的信息可以直接跳^交互介面
echo make apk's keystore file...
call keytool.exe -genkey -alias %ProjectKeyStore_Name% -keyalg RSA -validity %ValidityDays_Count% -keystore %ProjectKeyStore_Name% -dname "CN=%RD_Name%,OU=%Company_Name%,O=%Organization_Name%,L=%Local_Name%,ST=%Province_Name%,C=%Country_ID%" -keypass %MakeApk_Pass% -storepass %KeystoreMain_Pass%
if errorlevel 1 goto ERROR

:: 8. 用jarsigner 名cmdTest_unsigner.apk生成Target apk : cmdTest.apk
echo signed that unsigner apk file to make a *.apk file...
call jarsigner.exe -verbose -keystore %ProjectKeyStore_Name% -keypass %MakeApk_Pass% -storepass %KeystoreMain_Pass% -signedjar %ProjectOutApk_Name% %Project_Path%\bin\%ProjectUnsigedApk_Name% %ProjectKeyStore_Name% %MakeApk_Pass%
if errorlevel 1 goto ERROR
goto EXIT_SUCCESS

:ERROR
if exist %Success_Info% del %Success_Info%
echo some error occurred, please check!
goto EXIT_END

:EXIT_SUCCESS
echo make %ProjectOutApk_Name% success!
:: *******************************************************************************************************************************
:: OUTPUT Infomation and print in a file -------> make project's info
@echo off
echo ---------------------- > %Success_Info%
echo Total infomation:    * >> %Success_Info%
echo ---------------------- >> %Success_Info%
echo *************Project Info*************************************** >> %Success_Info%
echo Project path   : %Project_Path% >> %Success_Info%
echo Project Name   : %Project_Name% >> %Success_Info%
echo Package Name   : %ProjectPackage_Name% >> %Success_Info%
echo add-ons        : %ProjectSDK_ID% >> %Success_Info%
echo keystore       : %ProjectKeyStore_Name% >> %Success_Info%
echo keystore pass  : %MakeApk_Pass% >> %Success_Info%
echo keystore main pass: %KeystoreMain_Pass% >> %Success_Info%
echo Validity Days  : %ValidityDays_Count% >> %Success_Info%
echo *************Apk's Info***************************************** >> %Success_Info%
echo apk's Name     : %ProjectOutApk_Name% >> %Success_Info%
echo apk's Path     : %Project_Path%\%ProjectOutApk_Name% >> %Success_Info%
echo *************Developer Info************************************* >> %Success_Info%
echo Developer Name : %RD_Name% >> %Success_Info%
echo Company Name   : %Company_Name% >> %Success_Info%
echo Organization   : %Organization_Name% >> %Success_Info%
echo Country ID     : %Country_ID% >> %Success_Info%
echo *************END************************************************ >> %Success_Info%
:: save in file
::call start /wait %Success_Info%
@echo off
echo ----------------------------------------------------------------
echo Total infomation:                                              ^|
echo ----------------------------------------------------------------
echo *************Project Info***************************************
echo Project path   : %Project_Path%
echo Project Name   : %Project_Name%
echo Package Name   : %ProjectPackage_Name%
echo add-ons        : %ProjectSDK_ID%
echo keystore       : %ProjectKeyStore_Name%
echo keystore pass  : %MakeApk_Pass%
echo keystore main pass: %KeystoreMain_Pass%
echo Validity Days  : %ValidityDays_Count%
echo *************Apk's Info*****************************************
echo apk's Name     : %ProjectOutApk_Name%
echo apk's Path     : %Project_Path%\%ProjectOutApk_Name%
echo *************Developer Info*************************************
echo Developer Name : %RD_Name%
echo Company Name   : %Company_Name%
echo Organization   : %Organization_Name%
echo Country ID     : %Country_ID%
echo *************END************************************************
echo You can also see output file in :
echo %Success_Info%
goto EXIT_END

:EXIT_END
pause
exit
